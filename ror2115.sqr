! ROR2115
! NOHOME Survey Email
!***********************************************************************
! Purpose:
!
! This program pulls students with NOHOME requirement to determine whether
!  to send an email to the student to ask them to fill out a NOHOME 
!  Survey to select their home school.  The email is sent through 
!  FormFusion.
!
!***********************************************************************
! Parameters
!   p1  aid year
!
!***********************************************************************
! Modification History
! Who  When      What
! ---  --------- -------------------------------------------------------
! SLT  29-NOV-12 New SQR.
! SLT  25-MAR-13 Remove limitation on term for popsel.
! SLT  21-MAY-14 DBEU Changes
! SLT  22-Oct-18 Remove debug output.
!***********************************************************************
 
 
!|==========================================================================
!| Setup
!|==========================================================================
 
BEGIN-SETUP
   #include 'osu_includes.sqc'
   #define prog_name ROR2115
 
   #define TRUE 1
   #define FALSE 0
   #define SUCCESS 0
   #Define Test_Email_Address 'tanguays@onid.orst.edu'
   #Define Exclude_Codes ('DU01','DU02','DU03','DU04','DU0R','DU0S','DU0X')
 
END-SETUP

 
!|==========================================================================
!| Main Program
!|==========================================================================
 
BEGIN-PROGRAM

   DO initialization
   DO main
   Do close_files
   
END-PROGRAM
 
!---------------------------------------------------------------------------
 
BEGIN-PROCEDURE initialization

   input $filename    'Enter File Name '                  ! P1
   input $term        'Enter Term '                       ! P2
   input $print_days  'Enter No of days since last email' ! P3
   input $run_mode    'Enter Run Mode (A/U)'              ! P4
   
   Let $report_date = datetostr(datenow(),'DD-Mon-YYYY')
   Let $report_time = datetostr(datenow(),'HH24:MI')
   Let $survey_date = datetostr(datenow(),'DD-MON-YYYY')

   Let $sepchar = ','  ! Separator character (delimiter)
   LET $embedchar = '"'
    
   LET #record_count = 0
   LET #records_found = 0
   LET #print_days = $print_days
   LET $run_mode = upper($run_mode)
   
   OPEN $filename AS 55 for-writing RECORD=999999:vary STATUS=#file-status
   IF (#file-status != {SUCCESS})
      DISPLAY '*ERROR* opening ' noline
      DISPLAY $filename
      DISPLAY 'Error status = ' noline
      DISPLAY #file-status 8888888
   END-IF
   
   DO delete_popsel
   DO Get_Aid_Year
   
   DO GET_DATABASE_NAME ($db)
   Show 'running In DB ' $db 
   
   Let $application = 'FINAID'
   Let $selection = 'FA_DPP_HM_'||$aidy
   Let $creator_id = 'SAISUSR'
   Let $user_id = 'SAISPRD'

END-PROCEDURE

!---------------------------------------------------------------------------

BEGIN-PROCEDURE delete_popsel

BEGIN-SQL ON-ERROR=error_handler ('delete_popsel')
DELETE FROM GENERAL.GLBEXTR
WHERE GLBEXTR_APPLICATION  = $application
   AND GLBEXTR_SELECTION   = $selection
   AND GLBEXTR_CREATOR_ID  = $creator_id
   AND GLBEXTR_USER_ID     = $user_id
END-SQL
END-PROCEDURE

!---------------------------------------------------------------------------
Begin-Procedure Get_Aid_Year
let $aidy = ''
Begin-Select ON-ERROR=error_handler ('Get_Aid_Year')
ROVTERM_FA_PROC_YR        &aidy
   LET $aidy = &aidy
FROM ROVTERM
WHERE ROVTERM_CODE = $Term
End-Select
End-Procedure 
!------------------------------------------------------------
 
BEGIN-PROCEDURE main
BEGIN-SELECT ON-ERROR=error_handler ('main')
spriden_pidm                                               &pidm
spriden_id                                                 &sid
substr(spriden_first_name||' '||spriden_last_name,1,60)    &full_name  
spriden_first_name                                         &fname

  let #pidm = &pidm

  DO get_email
  IF $run_mode = 'U'
     DO insert_survey_popsel
  END-IF
  
  LET #record_count = #record_count + 1
  
  DO get_gurmail
!  SHOW #gurmail_found
  if not #gurmail_found
     LET #records_found = #records_found + 1
     DO get_survey
     DO write_file
     IF $run_mode = 'U'
        DO insert_gurmail
     END-IF
  end-if
  
FROM RRRAREQ, SPRIDEN
WHERE RRRAREQ_PIDM = SPRIDEN_PIDM
AND SPRIDEN_CHANGE_IND IS NULL
AND RRRAREQ_AIDY_CODE = $aidy
AND RRRAREQ_TREQ_CODE = 'NOHOME'
AND RRRAREQ_TRST_CODE = 'E'
AND EXISTS (SELECT 'X'
            FROM SFRSTCR, STVRSTS
            WHERE SFRSTCR_PIDM = RRRAREQ_PIDM
            AND STVRSTS_CODE = SFRSTCR_RSTS_CODE
            AND STVRSTS_INCL_SECT_ENRL = 'Y'
            AND STVRSTS_WITHDRAW_IND = 'N'
            AND SFRSTCR_TERM_CODE IN (SELECT STVTERM_CODE
                                      FROM STVTERM
                                      WHERE STVTERM_FA_PROC_YR = $aidy
                                      AND STVTERM_TRMT_CODE = '4'))
ORDER BY SPRIDEN_LAST_NAME, SPRIDEN_FIRST_NAME
END-SELECT

END-PROCEDURE

!---------------------------------------------------------------------------

BEGIN-PROCEDURE get_email

   LET $email = ' '

BEGIN-SELECT LOOPS=1 
goremal_email_address  &goremal_email

   LET $email = &goremal_email

FROM goremal, gtvsdax
WHERE goremal_pidm = #pidm
AND goremal_status_ind = 'A'
and gtvsdax_internal_code_group = 'OSU_EMAIL'
and gtvsdax_internal_code = upper('STUDENT')
and GTVSDAX_EXTERNAL_CODE = goremal_emal_code
ORDER BY decode(goremal_preferred_ind,'Y','0','1'),
         gtvsdax.gtvsdax_internal_code_seqno,
         to_char(goremal_activity_date, 'YYYYMMDD') desc 
END-SELECT

! when running in Development environment send to developer email  
if $db <> 'PROD'
   Show 'Changing email to test email address ' {Test_Email_Address}
   Show 'Prior address was ' $email 
   Let $email = {Test_Email_Address}
end-if 

END-PROCEDURE 

!------------------------------------------------------------
 
BEGIN-PROCEDURE get_gurmail

   LET #gurmail_found = 0

BEGIN-SELECT ON-ERROR=error_handler ('get_gurmail')
1                                                            &gurmail_found
(trunc(sysdate) - trunc(nvl(gurmail_date_printed,sysdate)))  &gurmail_print_days

!   SHOW 'PRINT DAYS: ' &gurmail_print_days
   
   LET #gurmail_found = &gurmail_found
   IF &gurmail_print_days > #print_days
       LET #gurmail_found = 0
   END-IF

FROM gurmail
WHERE gurmail_pidm = #pidm
AND gurmail_system_ind = 'R'
AND gurmail_letr_code = 'FAO_DPP_NOHOME'
AND gurmail_aidy_code = $aidy
AND gurmail_date_printed = (select max(gurmail_date_printed)
                              from gurmail
                             where gurmail_pidm = #pidm
                               and gurmail_letr_code = 'FAO_DPP_NOHOME'
                               and gurmail_aidy_code = $aidy)
END-SELECT
END-PROCEDURE
!------------------------------------------------------------
BEGIN-PROCEDURE write_file

      WRITE 55 FROM -
      $embedchar  $aidy          $embedchar   $sepchar -
      $embedchar  &sid           $embedchar   $sepchar -
      $embedchar  &full_name     $embedchar   $sepchar -
      $embedchar  $email         $embedchar   $sepchar -  
      $embedchar  &fname         $embedchar
 

         STATUS=#file-status
         IF (#file-status != {SUCCESS})
            DISPLAY '*ERROR* writing data record to ' noline
            DISPLAY $filename
            DISPLAY 'Error status = ' noline
            DISPLAY #file-status 8888888
         END-IF

END-PROCEDURE

!------------------------------------------------------------
 
BEGIN-PROCEDURE get_survey

BEGIN-SELECT ON-ERROR=error_handler ('get_survey')
GOBSRVR_DECLINED_IND                                       &DECLINED_IND
GOBSRVR_COMPLETE_IND                                       &COMPLETE_IND

   if (&DECLINED_IND = 'Y') OR (&COMPLETE_IND = 'Y')
   
      IF $run_mode = 'U'
         DO update_survey
      end-if
      
   end-if

FROM GOBSRVR
WHERE GOBSRVR_NAME = $selection
AND GOBSRVR_PIDM = #pidm

END-SELECT
END-PROCEDURE


!---------------------------------------------------------------------------

BEGIN-PROCEDURE update_survey

BEGIN-SQL ON-ERROR=error_handler ('update_survey')
UPDATE GOBSRVR
SET GOBSRVR_DECLINED_IND = 'N',
    GOBSRVR_COMPLETE_IND = 'N'
WHERE GOBSRVR_PIDM = &PIDM
AND GOBSRVR_NAME = $selection
END-SQL
END-PROCEDURE

!---------------------------------------------------------------------------

BEGIN-PROCEDURE insert_survey_popsel

BEGIN-SQL ON-ERROR=error_handler ('insert_survey_popsel')
INSERT INTO GLBEXTR
(
GLBEXTR_APPLICATION,
GLBEXTR_SELECTION,
GLBEXTR_CREATOR_ID,
GLBEXTR_USER_ID,
GLBEXTR_KEY,
GLBEXTR_ACTIVITY_DATE,
GLBEXTR_SYS_IND,
GLBEXTR_SLCT_IND
)
SELECT DISTINCT 
$application,
$selection,
$creator_id,
$user_id,
#pidm, 
SYSDATE,
'M',
NULL
FROM DUAL
END-SQL
END-PROCEDURE

!---------------------------------------------------------------------------
 
BEGIN-PROCEDURE insert_gurmail
 
BEGIN-SQL ON-ERROR=error_handler ('insert_gurmail')
INSERT INTO GURMAIL
(GURMAIL_PIDM,
GURMAIL_SYSTEM_IND,
GURMAIL_MODULE_CODE,
GURMAIL_LETR_CODE,
GURMAIL_DATE_INIT,
GURMAIL_DATE_PRINTED,
GURMAIL_USER,
GURMAIL_AIDY_CODE,
GURMAIL_ORIG_IND,
GURMAIL_PUB_GEN,
GURMAIL_ACTIVITY_DATE)
SELECT #pidm,
       'R',
       'R',
       'FAO_DPP_NOHOME',
       SYSDATE,
       SYSDATE,
       'ROR2115',
       $aidy,
       'S',
       'G',
       SYSDATE
       FROM SYS.DUAL
END-SQL
 
END-PROCEDURE

!------------------------------------------------------------
BEGIN-PROCEDURE close_files

   SHOW 'Number of students: ' #record_count
   SHOW 'Email Students:     ' #records_found
   
   CLOSE 55

   IF (#file-status != {SUCCESS})
      DISPLAY '*ERROR* closing ' noline
      DISPLAY $filename
      DISPLAY 'Error status = ' noline
      DISPLAY #file-status 8888888
      LET #return-status = 4
      STOP
   END-IF
   
   if $run_mode <> 'U'
      SHOW 'NOT IN UPDATE MODE - ROLLING BACK'
      rollback
   end-if


END-PROCEDURE
 
!---------------------------------------------------------------------------

!|==========================================================================
!| End of Report
!|==========================================================================
 
!---------------------------------------------------------------------------
 
BEGIN-PROCEDURE Error_Handler ($err_routine)
   IF #_sql-status <> 1    ! Unique constraint
      DISPLAY ''
      DISPLAY ''
      DISPLAY '*ERROR*  Procedure = ' noline
      DISPLAY $err_routine
      DISPLAY ''
      DISPLAY 'SQL Error #: ' noline
      DISPLAY #_sql-status 8888888
      DISPLAY $_sql-error
      DISPLAY ''
      LET #_return-status = 4
   END-IF
END-PROCEDURE
#include 'get_database_name.sqc'              ! get current db name